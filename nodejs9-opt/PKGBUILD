# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs9-opt
pkgver=9.11.2
pkgrel=5
pkgdesc='Evented I/O for V8 javascript'
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl-1.0' 'zlib' 'icu' 'libuv' 'http-parser' 'c-ares' 'libnghttp2') # 'v8')
makedepends=('python2' 'procps-ng')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz")
sha512sums=('e37e44e46b13bba1565119d57eff7b1ecd5d98c444d082cf27917a8c14eebf34de411f61dc76cee3bf503806ed509a9ed9d4538ce915421fc379abb702472d2e')

prepare() {
  cd node-$pkgver

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-$pkgver

  # http://site.icu-project.org/download/68#TOC-Migration-Issues
  CPPFLAGS+=' -DU_DEFINE_FALSE_AND_TRUE=1'

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export PYTHON=python2
  ./configure \
    --prefix=/opt/nodejs9 \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser \
    --shared-cares \
    --shared-nghttp2
    # --shared-v8

  make
}

check() {
  cd node-$pkgver
  # Expected failure: https://github.com/nodejs/node/issues/11627
  make test || warning "Tests failed"
}

package() {
  cd node-$pkgver

  # http://site.icu-project.org/download/68#TOC-Migration-Issues
  # Also needed for install phase since NodeJS always follows it's practices ;)
  CPPFLAGS+=' -DU_DEFINE_FALSE_AND_TRUE=1'

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
