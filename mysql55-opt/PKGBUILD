# Maintainer: Rustam Tsurik <rustam.tsurik@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mysql55-opt
_pkgname=mysql
true && pkgname=('libmysqlclient55-opt' 'mysql-clients55-opt' 'mysql55-opt')
pkgbase=mysql55-opt
pkgver=5.5.45
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="https://www.mysql.com/products/community/"
makedepends=('cmake' 'openssl' 'zlib')
options=('!libtool')
source=(http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.5/${_pkgname}-${pkgver}.tar.gz{,.asc}
        mysqld-post.sh
        mysqld-tmpfile.conf
        mysqld55-opt.service)
sha256sums=('744efd3c39265b5fdeebd25f58ecb34a6eb06e02d9ec2febd3164942f2c89332'
            'SKIP'
            '7203200662793368481e790f13ac65e22e8e29525bc67d886ec9601d543e4100'
            '6a14087a47365091b0ddb3d5d4245ab161f0036e7658d0c8e063644d0a6ff283'
            '4a480944926192da75e23783a8fe6f90dec8064a5019b9f248280e5de8b9875d')

build() {
  mkdir build
  cd build

  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/mysql55 \
    -DSYSCONFDIR=/opt/mysql55/etc \
    -DMYSQL_DATADIR=/opt/mysql55/var \
    -DMYSQL_UNIX_ADDR=/opt/mysql55/run/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_unicode_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=lib/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include \
    -DINSTALL_DOCREADMEDIR=share \
    -DINSTALL_SUPPORTFILESDIR=share \
    -DINSTALL_MYSQLSHAREDIR=share \
    -DINSTALL_DOCDIR=share/docs \
    -DINSTALL_SHAREDIR=share \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=bundled \
    -DWITH_SSL=bundled \
    -DWITH_LIBWRAP=OFF \
    -DWITH_MYSQLD_LDFLAGS="${LDFLAGS}" \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti"

  make
}

package_libmysqlclient55-opt(){
  pkgdesc="MySQL client libraries, v5.5"
  depends=('openssl')

  cd build
  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}"/opt/mysql55/bin
  install -m755 scripts/mysql_config "${pkgdir}"/opt/mysql55/bin/
  install -d "${pkgdir}"/opt/mysql55/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "${srcdir}"/${_pkgname}-${pkgver}/man/$man.1 "${pkgdir}"/opt/mysql55/share/man/man1/$man.1
  done
}

package_mysql-clients55-opt(){
  pkgdesc="MySQL client tools, v5.5"
  depends=('libmysqlclient55-opt')

  cd build
  make -C client DESTDIR="${pkgdir}" install

  # install man pages
  install -d "${pkgdir}"/opt/mysql55/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "${srcdir}"/${_pkgname}-${pkgver}/man/$man.1 "${pkgdir}"/opt/mysql55/share/man/man1/$man.1
  done

  # provided by mysql
  rm "${pkgdir}"/opt/mysql55/bin/{mysql_{plugin,upgrade},mysqlbinlog,mysqltest}
}

package_mysql55-opt(){
  pkgdesc="A fast SQL database server, v5.5"
  backup=('opt/mysql55/etc/my.cnf')
  install=mysql.install
  depends=('mysql-clients55-opt' 'systemd-tools')
  options=('emptydirs')

  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${pkgdir}"/opt/mysql55/share/my-medium.cnf "${pkgdir}"/opt/mysql55/etc/my.cnf
  install -Dm755 "${srcdir}"/mysqld-post.sh "${pkgdir}"/opt/mysql55/bin/mysqld-post
  install -Dm644 "${srcdir}"/mysqld-tmpfile.conf "${pkgdir}"/usr/lib/tmpfiles.d/mysqld55-opt.conf
  install -d "${pkgdir}"/usr/lib/systemd/system
  install -Dm644 "${srcdir}"/mysqld55-opt.service "${pkgdir}"/usr/lib/systemd/system/

  # provided by libmysqlclient
  rm "${pkgdir}"/opt/mysql55/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm "${pkgdir}"/opt/mysql55/lib/libmysql*
  rm -r "${pkgdir}"/opt/mysql55/include/
  rm "${pkgdir}"/opt/mysql55/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  
  # provided by mysql-clients
  rm "${pkgdir}"/opt/mysql55/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm "${pkgdir}"/opt/mysql55/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r "${pkgdir}"/opt/mysql55/{data,mysql-test,sql-bench}
  rm "${pkgdir}"/opt/mysql55/share/man/man1/mysql-test-run.pl.1

  #install -dm700 "${pkgdir}"/opt/mysql55/var
}

pkgdesc="A fast SQL database server, v5.5"
