pkgname=nodejs6-opt
pkgver=6.17.1
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl-1.0' 'zlib' 'icu' 'libuv' 'http-parser' 'c-ares') # 'v8')
makedepends=('python2' 'procps-ng')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz")
sha512sums=('59b9096810f3a924a344f838cf7a0690ed1eec40d0171a8e8c0f99520d3c4f5c5dece38c0ac843b3b831bacf941c1ab4ce74dcaa04cbf5222de01e00ca651b95')

prepare() {
  cd node-$pkgver

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-$pkgver

  # http://site.icu-project.org/download/68#TOC-Migration-Issues
  CPPFLAGS+=' -DU_DEFINE_FALSE_AND_TRUE=1'

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export PYTHON=python2
  ./configure \
    --prefix=/opt/nodejs6 \
    --with-intl=system-icu \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser \
    --shared-cares \
    # --shared-v8

  make
}

check() {
  cd node-$pkgver
  # Expected failure: https://github.com/nodejs/node/issues/11627
  make test || warning "Tests failed"
}

package() {
  cd node-$pkgver

  # http://site.icu-project.org/download/68#TOC-Migration-Issues
  # Also needed for install phase since NodeJS always follows it's practices ;)
  CPPFLAGS+=' -DU_DEFINE_FALSE_AND_TRUE=1'

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
