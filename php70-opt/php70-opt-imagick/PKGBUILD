_installdir="opt/php70"

pkgname=php70-opt-imagick
_extname=imagick
pkgver=3.4.3
pkgrel=7
pkgdesc="PHP extension for IMagick"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php70-opt' 'imagemagick>=6.2.4')
backup=("${_installdir}/etc/conf.d/${_extname}.ini")
source=("http://pecl.php.net/get/${_extname}-${pkgver}.tgz")
md5sums=('d0ee25c007cd2a28cefccc0b9ee63a28')

build() {
  cd "${_extname}-${pkgver}"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/${_installdir}/etc/conf.d/${_extname}.ini"
}
