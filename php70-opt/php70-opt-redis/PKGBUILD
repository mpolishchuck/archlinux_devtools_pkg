_installdir="opt/php70"

pkgname=php70-opt-redis
_extname=redis
pkgver=4.1.1
pkgrel=1
pkgdesc="PHP extension for redis"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php70-opt' 'redis')
backup=("${_installdir}/etc/conf.d/${_extname}.ini")
source=("http://pecl.php.net/get/${_extname}-${pkgver}.tgz")
md5sums=('32488f1d214e60afc5e027ae2d1e895f')

build() {
  cd "${_extname}-${pkgver}"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/${_installdir}/etc/conf.d/${_extname}.ini"
}
