_installdir="opt/php70"

pkgname=php70-opt-tideways-xhprof
pkgver=5.0BETA3
pkgrel=3
_commit=afc61bd1e3336ef207247c5e92ae6b5dab0a6e1c
pkgdesc="Tideways xhprof extension"
arch=('i686' 'x86_64')
url="https://tideways.com/"
license=('PHP')
depends=('php70-opt')
makedepends=('git')
backup=("${_installdir}/etc/conf.d/tideways_xhprof.ini")
#source=(https://pecl.php.net/get/memcache-$pkgver.tgz)
source=("git+https://github.com/tideways/php-xhprof-extension.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd "$srcdir/php-xhprof-extension"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "$srcdir/php-xhprof-extension"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=tideways_xhprof.so' >tideways_xhprof.ini
  install -Dm644 tideways_xhprof.ini "$pkgdir/${_installdir}/etc/conf.d/tideways_xhprof.ini"
}

# vim:set ts=2 sw=2 et:
