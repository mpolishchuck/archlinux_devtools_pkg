_installdir="opt/php70"

pkgname=php70-opt-xdebug
pkgver=2.5.1
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('i686' 'x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php70-opt')
options=('!makeflags')
backup=("${_installdir}/etc/conf.d/xdebug.ini")
pkg_src="xdebug-${pkgver}"
source=("https://www.xdebug.org/files/${pkg_src}.tgz"
	'xdebug.ini')
md5sums=('6167b1e104f1108d77f08eb561a12b22'
         '0e601dfb867b248f28d6a647611e4400')

build() {
  cd $srcdir/${pkg_src}
  "/${_installdir}/bin/phpize"
  ./configure --enable-xdebug --with-php-config="/${_installdir}/bin/php-config"
  make

  cd $srcdir/${pkg_src}/debugclient
  ./buildconf
  ./configure --prefix="/${_installdir}"
  make
}

package() {
  cd $srcdir/${pkg_src}/debugclient
  make DESTDIR=$pkgdir install

  cd $srcdir/${pkg_src}
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini "${pkgdir}/${_installdir}/etc/conf.d/xdebug.ini"
}
