_installdir="opt/php70"

pkgname=php70-opt-mongodb
_extname=mongodb
pkgver=1.5.2
pkgrel=1
pkgdesc="PHP extension for mongodb"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php70-opt')
backup=("${_installdir}/etc/conf.d/${_extname}.ini")
source=("http://pecl.php.net/get/${_extname}-${pkgver}.tgz")
md5sums=('3569a12839c9908a25dac15df1ae9d18')

build() {
  cd "${_extname}-${pkgver}"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/${_installdir}/etc/conf.d/${_extname}.ini"
}
