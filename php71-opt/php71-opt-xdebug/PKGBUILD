_installdir="opt/php71"

pkgname=php71-opt-xdebug
pkgver=2.7.1
pkgrel=3
pkgdesc="PHP debugging extension"
arch=('i686' 'x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php71-opt')
options=('!makeflags')
backup=("${_installdir}/etc/conf.d/xdebug.ini")
pkg_src="xdebug-${pkgver}"
source=("https://www.xdebug.org/files/${pkg_src}.tgz"
	'xdebug.ini')
md5sums=('8833252b258c22f73ca5ae1d51e668b6'
         '0e601dfb867b248f28d6a647611e4400')

build() {
  cd $srcdir/${pkg_src}
  "/${_installdir}/bin/phpize"
  ./configure --enable-xdebug --with-php-config="/${_installdir}/bin/php-config"
  make

  cd $srcdir/${pkg_src}/debugclient
  ./buildconf
  ./configure --with-libedit --prefix="/${_installdir}"
  make
}

package() {
  cd $srcdir/${pkg_src}/debugclient
  install -D -m 755 debugclient "${pkgdir}/${_installdir}/bin/debugclient"

  cd $srcdir/${pkg_src}
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini "${pkgdir}/${_installdir}/etc/conf.d/xdebug.ini"
}
