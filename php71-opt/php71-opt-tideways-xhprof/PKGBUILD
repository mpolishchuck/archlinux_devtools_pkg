_installdir="opt/php71"

pkgname=php71-opt-tideways-xhprof
pkgver=5.0.4
pkgrel=1
_commit=ab391914cd59b95ea1a6904eacad6960a696a50b
pkgdesc="Tideways xhprof extension"
arch=('i686' 'x86_64')
url="https://tideways.com/"
license=('PHP')
depends=('php71-opt')
makedepends=('git')
backup=("${_installdir}/etc/conf.d/tideways_xhprof.ini")
#source=(https://pecl.php.net/get/memcache-$pkgver.tgz)
source=("git+https://github.com/tideways/php-xhprof-extension.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd "$srcdir/php-xhprof-extension"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "$srcdir/php-xhprof-extension"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=tideways_xhprof.so' >tideways_xhprof.ini
  install -Dm644 tideways_xhprof.ini "$pkgdir/${_installdir}/etc/conf.d/tideways_xhprof.ini"
}

# vim:set ts=2 sw=2 et:
