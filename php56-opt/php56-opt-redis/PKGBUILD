# Contributor: Spencer Rinehart <anubis@overthemonkey.com>

_installdir="opt/php56"

pkgname=php56-opt-redis
_extname=redis
pkgver=2.2.8
pkgrel=7
pkgdesc="PHP extension for redis"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/${_extname}"
license=('PHP')
depends=('php56-opt' 'redis')
backup=("${_installdir}/etc/conf.d/${_extname}.ini")
source=("http://pecl.php.net/get/${_extname}-${pkgver}.tgz")
md5sums=('b6c998a6904cb89b06281e1cfb89bc4d')

build() {
  cd "${_extname}-${pkgver}"

  "/${_installdir}/bin/phpize"
  ./configure --with-php-config="/${_installdir}/bin/php-config"
  make
}

package() {
  cd "${_extname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  echo "extension=${_extname}.so" > "${_extname}.ini"
  install -D -m644 "${_extname}.ini" "${pkgdir}/${_installdir}/etc/conf.d/${_extname}.ini"
}
